// Project-Type
apply plugin: "cpp"

model {
    repositories {
        libs(PrebuiltLibraries) {
            easyloggingpp {
                headers.srcDir "easyloggingpp/src"
            }
            eigen {
                headers.srcDir "OpenNN/eigen/src"
            }
        }
    }

    components {
        tinyxml2(NativeLibrarySpec) {
            if(System.properties['sun.arch.data.model'] == "64") {
                targetPlatform "x64"
            } else {
                targetPlatform "x86"
            }

            sources.cpp {
                source {
                    srcDirs "OpenNN"
                    include "tinyxml2/**/*.cpp"
                }
                exportedHeaders {
                    srcDirs "OpenNN"
                    include "tinyxml2/**/*.h"
                }
            }
        }
        openNN(NativeLibrarySpec) {
            if(System.properties['sun.arch.data.model'] == "64") {
                targetPlatform "x64"
            } else {
                targetPlatform "x86"
            }

            sources.cpp {
                source {
                    srcDirs "OpenNN"
                    include "opennn/**/*.cpp"
                    exclude "opennn/informedness_optimization_threshold.cpp"
                }
                exportedHeaders {
                    srcDirs "OpenNN"
                    include "opennn/**/*.h"
                    exclude "opennn/informedness_optimization_threshold.h"
                }
                
                lib library: "eigen", linkage: "api"
                lib library: "tinyxml2", linkage: "static"
            }
        }
    }
    
    binaries {
        withType(SharedLibraryBinarySpec) {
            buildable = false
        }
        withType(StaticLibraryBinarySpec) {
            if (toolChain in Gcc) {
                cppCompiler.args "-std=c++1y", "-w", "-Ofast", "-g0"
            }
            if (toolChain in Clang) {
                cppCompiler.args "-std=c++1y", "-w", "-O0", "-g"
            }
            if (toolChain in VisualCpp) {
                cppCompiler.args "/std:c++14", "/W0", "/EHsc", "/O2"
            }
        }
    }
}
